/*
 * 画只龟
*/

使下 游戏

咩系 |龟|?
    佢个老豆叫 |公仔|
    
    佢识得 |__init__| |屏幕| -> {
        用下 |pygame.sprite.Sprite.__init__(self)|
        讲嘢 -> {
            |自己嘅 target_surfece| 係 |屏幕|
            |自己嘅 image| 係 |None|
            |自己嘅 序列图| 係 |None|
            |自己嘅 rect| 係 |None|
            |自己嘅 topleft| 係 |0, 0|
            |自己嘅 frame| 係 0
            |自己嘅 old_frame| 係 |0-1|
            |自己嘅 frame_width| 係 1
            |自己嘅 frame_height| 係 1
            |自己嘅 first_frame| 係 0
            |自己嘅 last_frame| 係 0
            |自己嘅 columns| 係 1
            |自己嘅 last_time| 係 0
        }
    }

    佢识得 加载 |图片路径, 宽, 高, 行| -> {
        |图片老作| 下 -> |图片路径| @ |自己嘅 序列图|
        讲嘢: -> {
            |自己嘅 frame_width| 係 宽
            |自己嘅 frame_height| 係 高
            |自己嘅 rect| 係 |500, 500, 宽, 高|
            |自己嘅 columns| 係 行
            |rect| 係 |喺边(自己嘅 序列图)|
            
            |自己嘅 last_frame| 係 |(rect.width // 宽) * (rect.height // 高) - 1|
        }
    }

    佢识得 |update| |current_time, rate = 60| -> {
        
        /*
         * 更新动画帧
        */

        如果 |current_time > 自己嘅 last_time + rate| 嘅话 -> {
            讲嘢: |自己嘅 frame| 係 |自己嘅 frame + 1|
            如果 |自己嘅 frame > 自己嘅 last_frame| 嘅话 -> {
                讲嘢: |自己嘅 frame| 係 |自己嘅 first_frame|
            }
            讲嘢: |自己嘅 last_time| 係 |current_time|
        }

        如果 |自己嘅 frame != 自己嘅 old_frame| 嘅话 -> {
            讲嘢 -> {
                frame_x 係 |(自己嘅 frame % 自己嘅 columns) * 自己嘅 frame_width|
                frame_y 係 |(自己嘅 frame // 自己嘅 columns) * 自己嘅 frame_height|
                rect 係 |(frame_x, frame_y, 自己嘅 frame_width, 自己嘅 frame_height )|
                |自己嘅 image| 係 |自己嘅 序列图.subsurface(rect)|
                |自己嘅 old_frame| 係 |自己嘅 frame|
            }
        }
    }

    佢识得 |跑步| |x, y| -> {
        讲嘢 -> {
            |自己嘅 rect| 係 |500 + x, 500, 自己嘅 frame_width, 自己嘅 frame_height|
            |自己嘅 rect| 係 |500, 500 + y, 自己嘅 frame_width, 自己嘅 frame_height|
        }
    }

明白未啊?

讲嘢 -> {
    x_change 係 0
    y_change 係 0
}

$ |移动| |e| 要做咩?
    
    Share下 x_change
    Share下 y_change
    
    如果 |移动(e) 系 100| 嘅话 -> {
        讲嘢: x_change 係 |x_change + 10|
    }

    如果 |移动(e) 系 97| 嘅话 -> {
        讲嘢: x_change 係 |x_change - 10|
    }

    如果 |移动(e) 系 119| 嘅话 -> {
        讲嘢: y_change 係 |y_change - 10|
    }

    如果 |移动(e) 系 115| 嘅话 -> {
        讲嘢: y_change 係 |y_change + 10|
    }

搞掂

有条仆街叫 |事件|
顶你 -> |事件| : |['KEYDOWN', 移动]|

讲嘢 |大小| 係 |(900, 700)|
|屏幕老作| 下 -> |大小, "龟龟大逃亡"| @ 屏幕

讲嘢: -> {
    |龟仔| 係 |龟(屏幕)|
    |公仔组| 係 |公仔集()|
    |framerate| 係 |计时器()|
}
龟仔 -> 加载: |"../img/turtle.png", 64, 64, 8|
用下 |嚟个公仔(公仔组, 龟仔)|

落操场玩跑步
    用下 |睇表(framerate, 300)|
    用下 |屏幕校色(屏幕, (0, 0, 100))|
    讲嘢: |间隔| 係 |pygame.time.get_ticks()|
    用下 |事件驱动(事件)|
    |龟仔| -> |跑步| : |x_change, y_change|

    用下 |刷新公仔(公仔组, 间隔)|
    用下 |画公仔(公仔组, 屏幕)|
    
    |刷新| 下 -> ||

玩到 唔啱 为止