/*
 * 画只龟
*/

使下 游戏

讲嘢: 得分 係 0
讲嘢: turtle位置 係 0

|| 拍住上 -> |移动范围|
|移动范围| -> |加啲|: 40
|移动范围| -> |加啲|: 232
|移动范围| -> |加啲|: 424
|移动范围| -> |加啲|: 616

咩系 |龟|?
    佢个老豆叫 |公仔|
    
    佢识得 |__init__| |屏幕| -> {
        用下 |pygame.sprite.Sprite.__init__(self)|
        讲嘢 -> {
            |自己嘅 target_surfece| 係 |屏幕|
            |自己嘅 image| 係 |None|
            |自己嘅 序列图| 係 |None|
            |自己嘅 rect| 係 |None|
            |自己嘅 topleft| 係 |0, 0|
            |自己嘅 frame| 係 0
            |自己嘅 old_frame| 係 |0-1|
            |自己嘅 frame_width| 係 1
            |自己嘅 frame_height| 係 1
            |自己嘅 first_frame| 係 0
            |自己嘅 last_frame| 係 0
            |自己嘅 columns| 係 1
            |自己嘅 last_time| 係 0
            |自己嘅 食嘢| 係 |嚟首sound("../music/eat.mp3")|
            |自己嘅 打洞| 係 |嚟首sound("../music/hit.mp3")|
            |自己嘅 胜利| 係 |唔啱|
        }
    }

    佢识得 加载 |图片路径, 宽, 高, 行| -> {
        |图片老作| 下 -> |图片路径| @ |自己嘅 序列图|
        讲嘢: -> {
            |自己嘅 frame_width| 係 宽
            |自己嘅 frame_height| 係 高
            |自己嘅 rect| 係 |640, 680, 宽, 高|
            |自己嘅 columns| 係 行
            |rect| 係 |喺边(自己嘅 序列图)|
            
            |自己嘅 last_frame| 係 |(rect.width // 宽) * (rect.height // 高) - 1|
        }
    }

    佢识得 |update| |current_time, rate = 60| -> {
        
        /*
         * 更新动画帧
        */

        如果 |current_time > 自己嘅 last_time + rate| 嘅话 -> {
            讲嘢: |自己嘅 frame| 係 |自己嘅 frame + 1|
            如果 |自己嘅 frame > 自己嘅 last_frame| 嘅话 -> {
                讲嘢: |自己嘅 frame| 係 |自己嘅 first_frame|
            }
            讲嘢: |自己嘅 last_time| 係 |current_time|
        }

        如果 |自己嘅 frame != 自己嘅 old_frame| 嘅话 -> {
            讲嘢 -> {
                frame_x 係 |(自己嘅 frame % 自己嘅 columns) * 自己嘅 frame_width|
                frame_y 係 |(自己嘅 frame // 自己嘅 columns) * 自己嘅 frame_height|
                rect 係 |(frame_x, frame_y, 自己嘅 frame_width, 自己嘅 frame_height )|
                |自己嘅 image| 係 |自己嘅 序列图.subsurface(rect)|
                |自己嘅 old_frame| 係 |自己嘅 frame|
            }
        }
    }

    佢识得 |跑步| |x, y| -> {
        
        Share下 dig
        Share下 打洞位置
        Share下 turtle位置
        
        |i| 从 0 行到 |打洞位置 嘅长度|
            如果 |(自己嘅 rect[0], 自己嘅 rect[1] + 64) 系 打洞位置[i]| 嘅话 -> {
                讲嘢 -> {
                    |自己嘅 rect| 係 |0, 0, 自己嘅 frame_width, 自己嘅 frame_height|
                }
            }
        行晒

        Share下 得分

        讲嘢 -> {
            |自己嘅 rect| 係 |640 + x, 616 + y, 自己嘅 frame_width, 自己嘅 frame_height|
            }

        |i| 从 0 行到 |食物位置 嘅长度|
                如果 |(自己嘅 rect[0], 自己嘅 rect[1]) 系 食物位置[i]| 嘅话 -> {
                    讲嘢： |食物位置[i]| 係 |(1088, 768)|
                    播放 下 -> |自己嘅 食嘢|
                    讲嘢: |得分| 係 |得分 + 1|
                } 
        行晒

        如果 |dig == 2| 嘅话 -> {
            播放 下 -> |自己嘅 打洞|
            讲嘢: dig 係 0
            如果 | not (((自己嘅 rect[0] + 64, 自己嘅 rect[1] + 64) 喺 _梯位))| 嘅话 -> {
                如果 | not (([自己嘅 rect[0] + 64, 自己嘅 rect[1] + 64] 喺 铁箱位置))| 嘅话 -> {
                    |打洞位置| -> |加啲|  |[自己嘅 rect[0] + 64, 自己嘅 rect[1] + 64, 0]|
                }
            }
        }

        如果 |dig == 1| 嘅话 -> {
            播放 下 -> |自己嘅 打洞|
            讲嘢: dig 係 0
            如果 | not (((自己嘅 rect[0] - 64, 自己嘅 rect[1] + 64) 喺 _梯位))| 嘅话 -> {
                如果 |not (([自己嘅 rect[0] - 64, 自己嘅 rect[1] + 64] 喺 铁箱位置))| 嘅话 -> {
                    |打洞位置| -> |加啲|  |[自己嘅 rect[0] - 64, 自己嘅 rect[1] + 64, 0]|
                }
            }
        }

        讲嘢: turtle位置 係 |自己嘅 rect|

        如果 |得分 系 6 同埋 turtle位置 系 (0, 40, 64, 64)| 嘅话 -> {
            讲嘢: |自己嘅 胜利| 係 啱
            讲嘢: |得分| 係 0
            || 拍住上 -> |打洞位置|
        }
    }

明白未啊?

讲嘢 -> {
    x_change 係 0
    y_change 係 0
    dig 係 0
    打洞位置 係 |[]|
}

$ |移动| |e| 要做咩?
    
    Share下 x_change
    Share下 y_change
    Share下 dig
    
    讲嘢: btn 係 |摞掣(e)|

    如果 |btn 系 100 或者 btn 系 1073741903| 嘅话 -> {
        如果 |turtle位置[0] + 64 < 宽| 嘅话 -> {
            如果 |turtle位置[1] 喺 移动范围| 嘅话 -> {
                如果 |not [turtle位置[0] + 64, turtle位置[1]] 喺 铁箱位置| 嘅话 -> {
                    讲嘢: x_change 係 |x_change + 64|
                }
            }
        }
    }

    如果 |btn 系 97 或者 btn 系 1073741904| 嘅话 -> {
        如果 |turtle位置[0] > 0| 嘅话 -> {
            如果 |turtle位置[1] 喺 移动范围| 嘅话 -> {
                如果 |not [turtle位置[0] - 64, turtle位置[1]] 喺 铁箱位置| 嘅话 -> {
                    讲嘢: x_change 係 |x_change - 64|
                }
            }
        }
    }

    如果 |btn 系 119 或者 btn 系 1073741906| 嘅话 -> {
        如果 |[turtle位置[0], turtle位置[1] - 64] 喺 _梯位2 或者 [turtle位置[0], turtle位置[1]] 喺 _梯位2| 嘅话 -> {    
            讲嘢: y_change 係 |y_change - 64|
        }
    }

    如果 |btn 系 115 或者 btn 系 1073741905| 嘅话 -> {
        如果 |[turtle位置[0], turtle位置[1] + 64] 喺 _梯位2| 嘅话 -> {    
            讲嘢: y_change 係 |y_change + 64|
        }
    }

    如果 |摞掣(e) 系 107| 嘅话 -> {
        讲嘢: dig 係 1
    }

    如果 |摞掣(e) 系 108| 嘅话 -> {
        讲嘢: dig 係 2
    }

搞掂