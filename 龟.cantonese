/*
 * 画只龟
*/

使下 游戏
使下 食物

咩系 |龟|?
    佢个老豆叫 |公仔|
    
    佢识得 |__init__| |屏幕| -> {
        用下 |pygame.sprite.Sprite.__init__(self)|
        讲嘢 -> {
            |自己嘅 target_surfece| 係 |屏幕|
            |自己嘅 image| 係 |None|
            |自己嘅 序列图| 係 |None|
            |自己嘅 rect| 係 |None|
            |自己嘅 topleft| 係 |0, 0|
            |自己嘅 frame| 係 0
            |自己嘅 old_frame| 係 |0-1|
            |自己嘅 frame_width| 係 1
            |自己嘅 frame_height| 係 1
            |自己嘅 first_frame| 係 0
            |自己嘅 last_frame| 係 0
            |自己嘅 columns| 係 1
            |自己嘅 last_time| 係 0
            |自己嘅 食嘢| 係 |嚟首sound("../music/eat.mp3")|
        }
    }

    佢识得 加载 |图片路径, 宽, 高, 行| -> {
        |图片老作| 下 -> |图片路径| @ |自己嘅 序列图|
        讲嘢: -> {
            |自己嘅 frame_width| 係 宽
            |自己嘅 frame_height| 係 高
            |自己嘅 rect| 係 |640, 640, 宽, 高|
            |自己嘅 columns| 係 行
            |rect| 係 |喺边(自己嘅 序列图)|
            
            |自己嘅 last_frame| 係 |(rect.width // 宽) * (rect.height // 高) - 1|
        }
    }

    佢识得 |update| |current_time, rate = 60| -> {
        
        /*
         * 更新动画帧
        */

        如果 |current_time > 自己嘅 last_time + rate| 嘅话 -> {
            讲嘢: |自己嘅 frame| 係 |自己嘅 frame + 1|
            如果 |自己嘅 frame > 自己嘅 last_frame| 嘅话 -> {
                讲嘢: |自己嘅 frame| 係 |自己嘅 first_frame|
            }
            讲嘢: |自己嘅 last_time| 係 |current_time|
        }

        如果 |自己嘅 frame != 自己嘅 old_frame| 嘅话 -> {
            讲嘢 -> {
                frame_x 係 |(自己嘅 frame % 自己嘅 columns) * 自己嘅 frame_width|
                frame_y 係 |(自己嘅 frame // 自己嘅 columns) * 自己嘅 frame_height|
                rect 係 |(frame_x, frame_y, 自己嘅 frame_width, 自己嘅 frame_height )|
                |自己嘅 image| 係 |自己嘅 序列图.subsurface(rect)|
                |自己嘅 old_frame| 係 |自己嘅 frame|
            }
        }
    }

    佢识得 |跑步| |x, y| -> {
        
        Share下 dig
        Share下 打洞位置
        
        |i| 从 0 行到 |打洞位置 嘅长度|
            如果 |(自己嘅 rect[0], 自己嘅 rect[1] + 64) 系 打洞位置[i]| 嘅话 -> {
                讲嘢 -> {
                |自己嘅 rect| 係 |0, 0, 自己嘅 frame_width, 自己嘅 frame_height|
                }
            }
        行晒

        讲嘢 -> {
            |自己嘅 rect| 係 |640 + x, 640 + y, 自己嘅 frame_width, 自己嘅 frame_height|
            }

        |i| 从 0 行到 |食物位置 嘅长度|
                如果 |(自己嘅 rect[0], 自己嘅 rect[1]) 系 食物位置[i]| 嘅话 -> {
                    讲嘢： |食物位置[i]| 係 |(1088, 768)|
                    播放 下 -> |自己嘅 食嘢|
                } 
        行晒

        如果 |dig == 2| 嘅话 -> {
            讲嘢: dig 係 0
            畀我睇下 |自己嘅 rect| 点样先?
            |打洞位置| -> |加啲|  |(自己嘅 rect[0] + 64, 自己嘅 rect[1] + 64)|
        }

        如果 |dig == 1| 嘅话 -> {
            讲嘢: dig 係 0
            畀我睇下 |自己嘅 rect| 点样先?
            |打洞位置| -> |加啲|  |(自己嘅 rect[0] - 64, 自己嘅 rect[1] + 64)|
        }

    }

明白未啊?

讲嘢 -> {
    x_change 係 0
    y_change 係 0
    dig 係 0
    打洞位置 係 |[]|
}

$ |移动| |e| 要做咩?
    
    Share下 x_change
    Share下 y_change
    Share下 dig
    
    如果 |摞掣(e) 系 100| 嘅话 -> {
        讲嘢: x_change 係 |x_change + 64|
    }

    如果 |摞掣(e) 系 97| 嘅话 -> {
        讲嘢: x_change 係 |x_change - 64|
    }

    如果 |摞掣(e) 系 119| 嘅话 -> {
        讲嘢: y_change 係 |y_change - 64|
    }

    如果 |摞掣(e) 系 115| 嘅话 -> {
        讲嘢: y_change 係 |y_change + 64|
    }

    如果 |摞掣(e) 系 107| 嘅话 -> {
        讲嘢: dig 係 1
    }

    如果 |摞掣(e) 系 108| 嘅话 -> {
        讲嘢: dig 係 2
    }

搞掂